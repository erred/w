---
description: things i found useful in zsh prompt
title: zsh prompt
---

### _prompt_

Maybe one day I'll get back to being a minimalist and just use `$` or `#` or `;`,
but right now I'm liking my custom prompt,
loosely based on [sindresorhus/pure](https://github.com/sindresorhus/pure).

```txt
22:49:16 ~ 0:00:00
»
```

#### _bit_ by bit

- start with a newline:
  this can be annoying as it takes up a lot of space,
  but saves you when the previous output doesn't end with a newline
- current time:
  not as smart as using a time updated when execution starts
  [example bash](https://redandblack.io/blog/2020/bash-prompt-with-updating-time/)
  but this is simpler and you can work out the same info by doing maths with the next prompt
- full path:
  very useful to orient yourself / copy-paste
- previous command execution time:
  how long it took for the previous command to execute,
  took me quite a while to collapse it into a 2 line thing.
- second line:
  yes it uses more vertical space,
  but I like having my commands consistently aligned within an environment
- screen:
  tells me if I'm in a screen session
- virtual env:
  tells me if I have a virtual env activated,
  not used much since I don't do much python these days
- git branch:
  current branch of git repo
- conditional coloring:
  red / magneta based on if the previous command exited properly,
  magneta because I like it.
- user@host:
  only shown if I'm ssh-ed into somewhere,
  otherwise it's redundant information
- `»`:
  my current preferred arrow,
  might cause issues if locale/termcap isn't set properly(?)

```sh
#!/usr/bin/env zsh

export PROMPT_EOL_MARK=''

function _preexec() {
    typeset -g prompt_timestamp=$EPOCHSECONDS
}

function _precmd() {
    integer elapsed=$(( EPOCHSECONDS - ${prompt_timestamp:-$EPOCHSECONDS} ))
    local human="$(( elapsed / 3600 )):${(l:2::0:)$(( elapsed / 60 % 60 ))}:${(l:2::0:)$(( elapsed % 60 ))}"
    vcs_info
    local newline=$'\n%{\r%}'

    PROMPT="${newline}%F{green}%*%f %F{blue}%~%f %F{yellow}${human}%f${newline}"
    PROMPT+="%F{242}${STY:+screen-}${VIRTUAL_ENV:+venv-}${vcs_info_msg_0_:+${vcs_info_msg_0_} }%f"
    PROMPT+="%(?.%F{magenta}.%F{red})${SSH_CONNECTION+%n@%m}»%f "
}

add-zsh-hook precmd  _precmd
add-zsh-hook preexec _preexec
```
